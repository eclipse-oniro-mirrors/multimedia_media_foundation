# Copyright (c) 2023-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
import("//foundation/multimedia/media_foundation/config.gni")
group("plugin_codec_adapter") {
  deps = []
  if (hst_is_lite_sys) {
    print("std hdi codec adapter is not available on lite systems")
  } else {
    deps += [ ":histreamer_plugin_CodecAdapter" ]
  }
}

config("plugin_codec_adapter_config") {
  include_dirs = [ "//foundation/multimedia/media_foundation/engine/include/" ]
}

ohos_source_set("std_plugin_codec_adapter") {
  subsystem_name = "multimedia"
  part_name = "media_foundation"
  sources = [
    "codec_buffer.cpp",
    "codec_buffer_pool.cpp",
    "codec_cmd_executor.cpp",
    "codec_port.cpp",
    "codec_utils.cpp",
    "hdi_codec_adapter.cpp",
    "hdi_codec_manager.cpp",
  ]
  public_deps = [
    "//foundation/multimedia/media_foundation/engine/foundation:histreamer_foundation",
    "//foundation/multimedia/media_foundation/engine/plugin:histreamer_plugin_base",
  ]
  external_deps = [ "hdf_core:libpub_utils" ]
  if (!hst_is_lite_sys) {
    external_deps += [
      "graphic_surface:surface",
      "hilog:libhilog",
    ]
  }
  public_configs = [
    ":plugin_codec_adapter_config",
    "//foundation/multimedia/media_foundation:histreamer_presets",
  ]
  public_external_deps = [
    "drivers_peripheral_codec:libcodec_client",
    "drivers_peripheral_codec:libcodec_hdi_omx_client",
    "drivers_peripheral_display:hdi_gralloc_client",
    "openmax:libopenmax_static",
  ]
}

if (hst_is_lite_sys) {
  print("std hdi codec adapter is not available on lite systems")
} else {
  import("//build/ohos.gni")
  ohos_shared_library("histreamer_plugin_CodecAdapter") {
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
      integer_overflow = true
    }
    deps = [ ":std_plugin_codec_adapter" ]

    external_deps = [
      "drivers_peripheral_codec:libcodec_hdi_omx_client",
      "hilog:libhilog",
    ]
    relative_install_dir = "media/histreamer_plugins"
    subsystem_name = "multimedia"
    part_name = "media_foundation"
  }
}
