# Copyright (c) 2021-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
import("//foundation/multimedia/media_foundation/config.gni")

group("plugin_stream_source") {
  deps = [ ":histreamer_plugin_StreamSource" ]
}

ohos_source_set("streamsource") {
  subsystem_name = "multimedia"
  part_name = "media_foundation"
  include_dirs = [ "//foundation/multimedia/media_foundation/engine/include" ]
  sources = [ "stream_source_plugin.cpp" ]
  public_deps = [
    "//foundation/multimedia/media_foundation/engine/foundation:histreamer_foundation",
    "//foundation/multimedia/media_foundation/engine/plugin:histreamer_plugin_base",
  ]
  public_configs =
      [ "//foundation/multimedia/media_foundation:histreamer_presets" ]
  if (hst_is_lite_sys) {
    public_deps += [ "//foundation/multimedia/media_utils_lite:media_common" ]
  }
  if (!hst_is_lite_sys) {
    external_deps = [ "hilog:libhilog" ]
  }
}

if (hst_is_lite_sys) {
  #lite
  import("//build/lite/config/component/lite_component.gni")
  lite_library("histreamer_plugin_StreamSource") {
    if (hst_is_mini_sys) {
      target_type = "static_library"
    } else {
      target_type = "shared_library"
    }
    deps = [ ":streamsource" ]
  }
} else {
  #standard
  import("//build/ohos.gni")
  ohos_shared_library("histreamer_plugin_StreamSource") {
    subsystem_name = "multimedia"
    part_name = "media_foundation"
    deps = [ ":streamsource" ]
    external_deps = [ "hilog:libhilog" ]
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
      integer_overflow = true
    }
    relative_install_dir = "media/histreamer_plugins"
  }
}
